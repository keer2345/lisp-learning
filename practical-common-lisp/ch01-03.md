
<!-- vim-markdown-toc GFM -->

* [1. Introduction: Why Lisp?](#1-introduction-why-lisp)
* [2. Lather, Rinse, Repeat: A Tour of the REPL](#2-lather-rinse-repeat-a-tour-of-the-repl)
    * [Choosing a Lisp Implementation](#choosing-a-lisp-implementation)
    * [Getting Up and Running with Lisp in a Box](#getting-up-and-running-with-lisp-in-a-box)
    * [Free Your Mind: Interactive Programming](#free-your-mind-interactive-programming)
    * [Experimenting in the REPL](#experimenting-in-the-repl)
    * ["Hello, World," Lisp Style](#hello-world-lisp-style)
    * [Saving Your Work](#saving-your-work)
* [3. Practical: A Simple Database](#3-practical-a-simple-database)
    * [CDs and Records](#cds-and-records)
    * [Filing CDs](#filing-cds)
    * [Looking at the Database Contents](#looking-at-the-database-contents)
    * [Improving the User Interaction](#improving-the-user-interaction)
    * [Saving and Loading the Database](#saving-and-loading-the-database)
    * [Querying the Database](#querying-the-database)
    * [Updating Existing Records--Another Use for WHERE](#updating-existing-records--another-use-for-where)
    * [Removing Duplication and Winning Big](#removing-duplication-and-winning-big)

<!-- vim-markdown-toc -->


# [1. Introduction: Why Lisp?](http://www.gigamonkeys.com/book/introduction-why-lisp.html)

- Why Lisp?
- Where It Began
- Who This Book Is For

# [2. Lather, Rinse, Repeat: A Tour of the REPL](http://www.gigamonkeys.com/book/lather-rinse-repeat-a-tour-of-the-repl.html)

## Choosing a Lisp Implementation
- Emacs + slime + sbcl
- [Portacle Emacs](https://portacle.github.io/)
## Getting Up and Running with Lisp in a Box
## Free Your Mind: Interactive Programming
```
wget https://github.com/portacle/portacle/releases/download/1.3b/lin-portacle.tar.xz
xz -d lin-portacle.tar.xz
cd portacle
./portacle.run
```

## Experimenting in the REPL
```lisp
CL-USER> 10
10
CL-USER> (+ 2 3)
5
```
## "Hello, World," Lisp Style
```lisp
CL-USER> "hello, world"
"hello, world"
CL-USER> (format t "hello, world")
hello, world
NIL
```
```lisp
CL-USER> (defun hello-world () (format t "hello, world"))
HELLO-WORLD
CL-USER> (hello-world)
hello, world
NIL
```

## Saving Your Work
```
mkdir -p codes/ch02
touch hello-world.lisp
```

```lisp
(defun hello-world ()
  (format t "Hello, world!"))
```

Run Command with `M-x`:
```
slime-compile-defun
```
```lisp
CL-USER> (hello-world)
Hello, world!
NIL
```



If the file is modified, use `load`:
```lisp
CL-USER> (load "hello-world.lisp")
T
CL-USER> (hello-world)
Hello, world!
NIL
```

# [3. Practical: A Simple Database](http://www.gigamonkeys.com/book/practical-a-simple-database.html)

## CDs and Records
Common Lisp gives you lots of choices of data structures from a simple four-item list to a user-defined class, using the Common Lisp Object System (CLOS).

For now you can stay at the simple end of the spectrum and use a list. You can make a list with the **LIST** function, which, appropriately enough, returns a list of its arguments.

```lisp
CL-USER> (list 1 2 3)
(1 2 3)
```
A keyword is any name that starts with a colon (`:`), for instance, `:foo`. Here's an example of a plist using the keyword symbols `:a`, `:b`, and :c as property names:
```lisp
CL-USER> (list :a 1 :b 2 :c 3)
(:A 1 :B 2 :C 3)
```

The function **GETF**, which takes a plist and a symbol and returns the value in the plist following the symbol, making a plist a sort of poor man's hash table:
```lisp
CL-USER> (getf(list :a 1 :b 2 :c 3) :a)
1
CL-USER> (getf(list :a 1 :b 2 :c 3) :c)
3
```

Given all that, you can easily enough write a function make-cd that will take the four fields as arguments and return a plist representing that CD:
```lisp
CL-USER> (defun make-cd (title artist rating ripped)
	   (list :title title :artist artist :rating rating :ripped ripped))
MAKE-CD
CL-USER> (make-cd "Roses" "Kathy Mattea" 7 t)
(:TITLE "Roses" :ARTIST "Kathy Mattea" :RATING 7 :RIPPED T)
```

## Filing CDs
A single record, however, does not a database make. You need some larger construct to hold the records.

For simplicity you can use a global variable, `*db*`, which you can define with the **DEFVAR** macro. The asterisks (`*`) in the name are a Lisp naming convention for **global variables**.

You can use the **PUSH** macro to add items to *db*.
```lisp
CL-USER> (defvar *db* nil)
*DB*
CL-USER> (defun add-record (cd) (push cd *db* ))
ADD-RECORD
```
```lisp
CL-USER> (add-record (make-cd "Roses" "Kathy Mattea" 7 t))
((:TITLE "Roses" :ARTIST "Kathy Mattea" :RATING 7 :RIPPED T))
CL-USER> (add-record (make-cd "Fly" "Dixie Chicks" 8 t))
((:TITLE "Fly" :ARTIST "Dixie Chicks" :RATING 8 :RIPPED T)
 (:TITLE "Roses" :ARTIST "Kathy Mattea" :RATING 7 :RIPPED T))
CL-USER> (add-record (make-cd "Home" "Dixie Chicks" 9 t))
((:TITLE "Home" :ARTIST "Dixie Chicks" :RATING 9 :RIPPED T)
 (:TITLE "Fly" :ARTIST "Dixie Chicks" :RATING 8 :RIPPED T)
 (:TITLE "Roses" :ARTIST "Kathy Mattea" :RATING 7 :RIPPED T))
```

## Looking at the Database Contents
```lisp
CL-USER> *db*
((:TITLE "Home" :ARTIST "Dixie Chicks" :RATING 9 :RIPPED T)
 (:TITLE "Fly" :ARTIST "Dixie Chicks" :RATING 8 :RIPPED T)
 (:TITLE "Roses" :ARTIST "Kathy Mattea" :RATING 7 :RIPPED T))

CL-USER> (defun dump-db ()
	   (dolist (cd *db*)
	     (format t "~{~a:~10t~a~%~}~%" cd)))
WARNING: redefining COMMON-LISP-USER::DUMP-DB in DEFUN
DUMP-DB
CL-USER> (dump-db)
TITLE:    Home
ARTIST:   Dixie Chicks
RATING:   9
RIPPED:   T

TITLE:    Fly
ARTIST:   Dixie Chicks
RATING:   8
RIPPED:   T

TITLE:    Roses
ARTIST:   Kathy Mattea
RATING:   7
RIPPED:   T

NIL
```

- **DOLIST**
- **FORMAT**
    - `t` is shorthand for the stream `*standard-output*`
    - between `~{` and `~}` the argument to be consumed must be a list
    - `~%` directive doesn't consume any arguments but to emit a newline.
    - the last `~%` to emit one more newline to put a blank line between each CD.

```lisp
CL-USER> (format t "~a" "Dixie Chicks")
Dixie Chicks
NIL
CL-USER> (format t "~a" :title)
TITLE
NIL
CL-USER> (format t "~a:~10t~a" :artist "Dixie Chicks")
ARTIST:   Dixie Chicks
NIL
CL-USER> (format t "~{~a:~10t~a~%~}~%" (list :TITLE "Roses" :ARTIST "Kathy Mattea" :RATING 7 :RIPPED T))
TITLE:    Roses
ARTIST:   Kathy Mattea
RATING:   7
RIPPED:   T

NIL
```

```lisp
CL-USER> (defun dump-db ()
	   (format t "~{~{~a:~10t~a~%~}~%~}" *db*))
WARNING: redefining COMMON-LISP-USER::DUMP-DB in DEFUN
DUMP-DB
CL-USER> (dump-db)
TITLE:    Home
ARTIST:   Dixie Chicks
RATING:   9
RIPPED:   T

TITLE:    Fly
ARTIST:   Dixie Chicks
RATING:   8
RIPPED:   T

TITLE:    Roses
ARTIST:   Kathy Mattea
RATING:   7
RIPPED:   T

NIL
```

## Improving the User Interaction
```lisp
CL-USER> (defun prompt-read (prompt)
	   (format *query-io* "~a:" prompt)
	   (force-output *query-io*)
	   (read-line *query-io*))
PROMPT-READ
```
- **FORCE-OUTPUT** is necessary in some implementations to ensure that Lisp doesn't wait for a newline before it prints the prompt.

```lisp
CL-USER> (defun prompt-for-cd ()
	   (make-cd
	    (prompt-read "Title")
	    (prompt-read "Artist")
	    (or (parse-integer(prompt-read "Rating") :junk-allowed t) 0)
	    (y-or-n-p "Ripped [y/n]:")))
PROMPT-FOR-CD
```

- **PARSE-INTEGER** is to signal an error if it can't parse an integer out of the string or if there's any non-numeric junk in the string. However, it takes an optional keyword argument `:junk-allowed`, which tells it to relax a bit.

You can use the simple form of the **LOOP** macro, which repeatedly executes a body of expressions until it's exited by a call to **RETURN**. For example:
```lisp
CL-USER> (defun add-cds ()
	   (loop (add-record (prompt-for-cd))
	      (if (not (y-or-n-p "Another? [y/n]: ")) (return))))
ADD-CDS
```
```lisp
CL-USER> (add-cds)
Title: Rockin' the Suburbs
Artist: Ben Folds
Rating: 6
Ripped  [y/n]: y
Another?  [y/n]: y
Title: Give Us a Break
Artist: Limpopo
Rating: 10
Ripped  [y/n]: y
Another?  [y/n]: y
Title: Lyle Lovett
Artist: Lyle Lovett
Rating: 9
Ripped  [y/n]: y
Another?  [y/n]: n
NIL
```


## Saving and Loading the Database
```lisp
CL-USER> (defun save-db (filename)
	   (with-open-file (out
			    filename
			    :direction
			    :output
			    :if-exists
			    :supersede)
	     (with-standard-io-syntax
	       (print *db* out))))
SAVE-DB
CL-USER> (save-db "~/my-cds.db")
((:TITLE "this is \" lsjdf" :ARTIST "vovo" :RATING 99 :RIPPED T)
 (:TITLE "Lyle Lovett" :ARTIST "Lyle Lovett" :RATING 0 :RIPPED NIL)
 (:TITLE "Give Us a Breadk" :ARTIST "Limpopo" :RATING 10 :RIPPED T)
 (:TITLE "Rockin' the Suburbs" :ARTIST "Ben Folds" :RATING 6 :RIPPED T)
 (:TITLE "Home" :ARTIST "Dixie Chicks" :RATING 9 :RIPPED T)
 (:TITLE "Fly" :ARTIST "Dixie Chicks" :RATING 8 :RIPPED T)
 (:TITLE "Roses" :ARTIST "Kathy Mattea" :RATING 7 :RIPPED T))
```

> On Windows, the filename might be something like `"c:/my-cds.db"` or `"c:\\my-cds.db."`

```lisp
CL-USER> (defun load-db (filename)
	   (with-open-file (in filename)
	     (with-standard-io-syntax
	       (setf *db* (read in)))))
LOAD-DB
CL-USER> (load-db "~/my-cds.db")
((:TITLE "this is \" lsjdf" :ARTIST "vovo" :RATING 99 :RIPPED T)
 (:TITLE "Lyle Lovett" :ARTIST "Lyle Lovett" :RATING 0 :RIPPED NIL)
 (:TITLE "Give Us a Breadk" :ARTIST "Limpopo" :RATING 10 :RIPPED T)
 (:TITLE "Rockin' the Suburbs" :ARTIST "Ben Folds" :RATING 6 :RIPPED T)
 (:TITLE "Home" :ARTIST "Dixie Chicks" :RATING 9 :RIPPED T)
 (:TITLE "Fly" :ARTIST "Dixie Chicks" :RATING 8 :RIPPED T)
 (:TITLE "Roses" :ARTIST "Kathy Mattea" :RATING 7 :RIPPED T))
```

- The **SETF** macro is Common Lisp's main assignment operator. It sets its first argument to the result of evaluating its second argument.

## Querying the Database
What you need is a way to query the database. You might like, for instance, to be able to write something like this:
```lisp
(select :artist "Dixie Chicks")
```
The function **REMOVE-IF-NOT** takes a predicate and a list and returns a list containing only the elements of the original list that match the predicate. **REMOVE-IF-NOT** doesn't really remove anything--it creates a new list, leaving the original list untouched.
```lisp
CL-USER> (remove-if-not #'evenp '(1 2 3 4 5 6 7 8 9 10))
(2 4 6 8 10)
```
- The function **EVENP**, which returns true if its argument is an even number.
- The funny notation `#'` is shorthand for "*Get me the function with the following name.*" Without the `#'`, Lisp would treat evenp as the name of a variable and look up the value of the variable, not the function.

Also, you can pass **REMOVE-IF-NOT** an anonymous function. For instance, if **EVENP** didn't exist, you could write the previous expression as the following:
```lisp
CL-USER> (remove-if-not #'(lambda (x) (= 0 (mod x 2))) '(1 2 3 4 5 6 7 8 9 10))
(2 4 6 8 10)
CL-USER> (remove-if-not #'(lambda (x) (= 1 (mod x 2))) '(1 2 3 4 5 6 7 8 9 10))
(1 3 5 7 9)
```

- **LAMBDA** expression looks a lot like a **DEFUN**: the word `lambda` is followed by a parameter list, which is followed by the body of the function.
- The function **GETF** can extract named fields from a plist. So assuming cd is the name of a variable holding a single database record, you can use the expression `(getf cd :artist)` to extract the name of the artist.
- The function **EQUAL**, when given string arguments, compares them character by character. So `(equal (getf cd :artist) "Dixie Chicks")` will test whether the artist field of a given CD is equal to "*Dixie Chicks*".

```lisp
CL-USER>  (remove-if-not #'(lambda (cd) (equal (getf cd :artist) "Dixie Chicks")) *db*)
((:TITLE "Home" :ARTIST "Dixie Chicks" :RATING 9 :RIPPED T)
 (:TITLE "Fly" :ARTIST "Dixie Chicks" :RATING 8 :RIPPED T))
```
```lisp
CL-USER> (defun select-by-artist (artist)
	   (remove-if-not
	    #'(lambda (cd) (equal (getf cd :artist) artist))
	    *db*))
SELECT-BY-ARTIST
CL-USER> (select-by-artist "Dixie Chicks")
((:TITLE "Home" :ARTIST "Dixie Chicks" :RATING 9 :RIPPED T)
 (:TITLE "Fly" :ARTIST "Dixie Chicks" :RATING 8 :RIPPED T))
```
```lisp
CL-USER> (defun select (selector-fn)
	   (remove-if-not selector-fn *db*))
SELECT
CL-USER> (select #'(lambda (cd) (equal (getf cd :artist) "Dixie Chicks")))
((:TITLE "Home" :ARTIST "Dixie Chicks" :RATING 9 :RIPPED T)
 (:TITLE "Fly" :ARTIST "Dixie Chicks" :RATING 8 :RIPPED T))
```



But that's really quite gross-looking. Luckily, you can wrap up the creation of the anonymous function:
```lisp
CL-USER> (defun artist-selector (artist)
	   #'(lambda (cd) (equal (getf cd :artist) artist)))
ARTIST-SELECTOR
CL-USER> (select (artist-selector "Dixie Chicks"))
((:TITLE "Home" :ARTIST "Dixie Chicks" :RATING 9 :RIPPED T)
 (:TITLE "Fly" :ARTIST "Dixie Chicks" :RATING 8 :RIPPED T))
```

Now you just need some more functions to generate selectors. But just as you don't want to have to write `select-by-title`, `select-by-rating`. For instance, the following function:
```lisp
CL-USER> (defun foo (a b c) (list a b c))
FOO
CL-USER> (defun foo (&key a b c) (list a b c))
FOO
CL-USER> (foo :a 1 :b 2 :c 3)
(1 2 3)
CL-USER> (foo :c 3 :b 2 :a 1)
(1 2 3)
CL-USER> (foo :a 1 :c  3)
(1 NIL 3)
CL-USER> (foo)
(NIL NIL NIL)
```

Normally if a function is called with no argument for a particular keyword parameter, the parameter will have the value **NIL**. However, sometimes you'll want to be able to distinguish between a **NIL** that was explicitly passed as the argument to a keyword parameter and the default value **NIL**.

To allow this, when you specify a keyword parameter you can replace the simple name with a list consisting of the name of the parameter, a default value, and another parameter name, called a **_supplied-p_** parameter.

The supplied-p parameter will be set to true or false depending on whether an argument was actually passed for that keyword parameter in a particular call to the function. Here's a version of `foo` that uses this feature:
```lisp
CL-USER> (defun foo (&key a (b 20) (c 30 c-p)) (list a b c c-p))
FOO
CL-USER> (foo :a 1 :b 2 :c 3)
(1 2 3 T)
CL-USER> (foo :c 3 :b 2 :a 1)
(1 2 3 T)
CL-USER> (foo :a 1 :c 3)
(1 20 3 T)
CL-USER> (foo)
(NIL 20 30 NIL)
```

The general selector-function generator, which you can call where for reasons that will soon become apparent if you're familiar with SQL databases. For instance, it will let you say things like this:
```lisp
(select (where :artist "Dixie Chicks"))
(select (where :rating 10 :ripped nil))
```

The function looks like this:

```lisp
CL-USER>  (defun where (&key title artist rating (ripped nil ripped-p))
	   #'(lambda (cd)
	       (and
		(if title (equal (getf cd :title) title) t)
		(if artist (equal (getf cd :artist) artist) t)
		(if rating (equal (getf cd :rating) rating) t)
		(if ripped-p (equal (getf cd :ripped) ripped) t))))
WHERE
CL-USER> (select (where :artist "Dixie Chicks"))
((:TITLE "Home" :ARTIST "Dixie Chicks" :RATING 9 :RIPPED T)
 (:TITLE "Fly" :ARTIST "Dixie Chicks" :RATING 8 :RIPPED T))
CL-USER> (select (where :artist "vovo" :rating 99))
((:TITLE "this is \" lsjdf" :ARTIST "vovo" :RATING 99 :RIPPED T))
CL-USER> (select (where :rating 0 :ripped nil))
((:TITLE "Lyle Lovett" :ARTIST "Lyle Lovett" :RATING 0 :RIPPED NIL))
CL-USER>  (select (where :artist "vovo" :ripped t))
((:TITLE "this is \" lsjdf" :ARTIST "vovo" :RATING 77 :RIPPED T)
 (:TITLE "this is \" lsjdf" :ARTIST "vovo" :RATING 99 :RIPPED T))
```

- Each clause checks if the appropriate argument was passed in and then either compares it to the value in the corresponding field in the CD record or returns **t**.


## Updating Existing Records--Another Use for WHERE
The main new bit is the use of a function **MAPCAR** that maps over a list, `*db*` in this case, and returns a new list containing the results of calling a function on each item in the original list.
```lisp
CL-USER> (defun update (selector-fn &key title artist rating (ripped nil ripped-p))
          (setf *db*
            (mapcar
             #'(lambda (row)
                 (when (funcall selector-fn row)
                   (if title    (setf (getf row :title) title))
                   (if artist   (setf (getf row :artist) artist))
                   (if rating   (setf (getf row :rating) rating))
                   (if ripped-p (setf (getf row :ripped) ripped)))
                 row) *db*)))
UPDATE
CL-USER> (update (where :artist "Dixie Chicks") :rating 11)
((:TITLE "this is \" lsjdf" :ARTIST "vovo" :RATING 77 :RIPPED T)
 (:TITLE "this is \" lsjdf" :ARTIST "vovo" :RATING 99 :RIPPED T)
 (:TITLE "Lyle Lovett" :ARTIST "Lyle Lovett" :RATING 0 :RIPPED NIL)
 (:TITLE "Give Us a Breadk" :ARTIST "Limpopo" :RATING 10 :RIPPED T)
 (:TITLE "Rockin' the Suburbs" :ARTIST "Ben Folds" :RATING 6 :RIPPED T)
 (:TITLE "Home" :ARTIST "Dixie Chicks" :RATING 11 :RIPPED T)
 (:TITLE "Fly" :ARTIST "Dixie Chicks" :RATING 11 :RIPPED T)
 (:TITLE "Roses" :ARTIST "Kathy Mattea" :RATING 7 :RIPPED T))
CL-USER> (select (where :artist "Dixie Chicks"))
((:TITLE "Home" :ARTIST "Dixie Chicks" :RATING 11 :RIPPED T)
 (:TITLE "Fly" :ARTIST "Dixie Chicks" :RATING 11 :RIPPED T))
```
You can even more easily add a function to delete rows from the database:
```lisp
CL-USER> (defun delete-rows (selector-fn)
	   (setf *db* (remove-if selector-fn *db*)))
DELETE-ROWS
CL-USER> (delete-rows (where :title "Roses"))
((:TITLE "this is \" lsjdf" :ARTIST "vovo" :RATING 77 :RIPPED T)
 (:TITLE "this is \" lsjdf" :ARTIST "vovo" :RATING 99 :RIPPED T)
 (:TITLE "Lyle Lovett" :ARTIST "Lyle Lovett" :RATING 0 :RIPPED NIL)
 (:TITLE "Give Us a Breadk" :ARTIST "Limpopo" :RATING 10 :RIPPED T)
 (:TITLE "Rockin' the Suburbs" :ARTIST "Ben Folds" :RATING 6 :RIPPED T)
 (:TITLE "Home" :ARTIST "Dixie Chicks" :RATING 11 :RIPPED T)
 (:TITLE "Fly" :ARTIST "Dixie Chicks" :RATING 11 :RIPPED T))
```

## Removing Duplication and Winning Big
```lisp
CL-USER> (select (where :title "Give Us a Break" :ripped t))
((:TITLE "Give Us a Break" :ARTIST "Limpopo" :RATING 10 :RIPPED T))
```
```lisp
CL-USER> (select
	  #'(lambda (cd)
	      (and (equal (getf cd :title) "Give Us a Break")
		   (equal (getf cd :ripped) t))))
((:TITLE "Give Us a Break" :ARTIST "Limpopo" :RATING 10 :RIPPED T))
```
Note that the anonymous function is different from the one that `where` would have returned; you're not trying to save the call to `where` but rather to provide a more efficient selector function. This anonymous function has clauses only for the fields that you actually care about at this call site, so it doesn't do any extra work the way a function returned by `where` might.

You can probably imagine going through all your source code and fixing up all the calls to `where` in this way. But you can probably also imagine that it would be a huge pain. If there were enough of them, and it was important enough, it might even be worthwhile to write some kind of preprocessor that converts `where` calls to the code you'd write by hand.

The Lisp feature that makes this trivially easy is its **macro** system. a Lisp macro is essentially a code generator that gets run for you automatically by the compiler.12 When a Lisp expression contains a call to a macro, instead of evaluating the arguments and passing them to the function, the Lisp compiler passes the arguments, unevaluated, to the macro code, which returns a new Lisp expression that is then evaluated in place of the original macro call.
```lisp
CL-USER> (reverse '(1 2 3))
(3 2 1)
CL-USER> (defmacro backwards (expr) (reverse expr))
BACKWARDS
CL-USER> (backwards ("hello, world" t format))
hello, world
NIL
```
How did that work? When the REPL started to evaluate the `backwards` expression, it recognized that backwards is the name of a macro. So it left the expression `("hello, world" t format)` unevaluated, which is good because it isn't a legal Lisp form.

It then passed that list to the `backwards` code. The code in backwards passed the list to **REVERSE**, which returned the list `(format t "hello, world")`. `backwards` then passed that value back out to the REPL, which then evaluated it in place of the original expression.

You had an expression of the following form for each actual field referred to in the original call to `where`:
```lisp
(equal (getf cd field) value)
```

So let's write a function that, given the name of a field and a value, returns such an expression. Since an expression is just a list, you might think you could write something like this:

```
(defun make-comparison-expr (field value)    ; wrong
  (list equal (list getf cd field) value))
```

However, you also know how to stop Lisp from evaluating a form: stick a single forward quote (`'`) in front of it. So if you write `make-comparison-expr` like this, it will do what you want:
```lisp
CL-USER> (defun make-comparison-expr (field value)
	   (list 'equal (list 'getf 'cd field) value))
MAKE-COMPARISON-EXPR
CL-USER> (make-comparison-expr :rating 10)
(EQUAL (GETF CD :RATING) 10)
CL-USER> (make-comparison-expr :title "Give Us a Break")
(EQUAL (GETF CD :TITLE) "Give Us a Break")
```

It turns out that there's an even better way to do it. What you'd really like is a way to write an expression that's mostly not evaluated and then have some way to pick out a few expressions that you do want evaluated. And, of course, there's just such a mechanism. A back quote ( \` ) before an expression stops evaluation just like a forward quote.
```lisp
CL-USER> `(1 2 3)
(1 2 3)
CL-USER> '(1 2 3)
(1 2 3)
```
However, in a back-quoted expression, any subexpression that's preceded by a comma is evaluated. Notice the effect of the comma in the second expression:
```lisp
CL-USER> `(1 2 (+ 1 2))
(1 2 (+ 1 2))
CL-USER> `(1 2 ,(+ 1 2))
(1 2 3)
```

Using a back quote, you can write `make-comparison-expr` like this:
```lisp
CL-USER> (defun make-comparison-expr (field value)
	   `(equal (getf cd ,field) ,value))
MAKE-COMPARISON-EXPR
```

You can dip into the bag of advanced Lisp tricks and pull out the mighty and powerful LOOP macro:
```lisp
CL-USER> (defun make-comparisons-list (fields)
	   (loop while fields
	      collecting (make-comparison-expr (pop fields) (pop fields))))
MAKE-COMPARISONS-EXPR
```
This **LOOP** expression does exactly what you need:
- it loops while there are elements left in the `fields` list, popping off two at a time, passing them to `make-comparison-expr`
- collecting the results to be returned at the end of the loop
- The **POP** macro performs the inverse operation of the `PUSH` macro you used to add records to `*db*`.

Now you just need to wrap up the list returned by `make-comparison-list` in an `AND` and an anonymous function, which you can do in the `where` macro itself. Using a back quote to make a template that you fill in by interpolating the value of `make-comparisons-list`, it's trivial.
```lisp
CL-USER> (defmacro where (&rest clauses)
	   `#'(lambda (cd) (and ,@(make-comparisons-list clauses))))
WHERE
```
This macro uses a variant of , (namely, the ,@) before the call to `make-comparisons-list`.  The `,@` "splices" the value of the following expression--which must evaluate to a list--into the enclosing list. 

You can see the difference between `,` and `,@` in the following two expressions:
```lisp
CL-USER> `(and ,(list 1 2 3))
(AND (1 2 3))
CL-USER> `(and ,@(list 1 2 3))
(AND 1 2 3)
CL-USER> `(and ,@(list 1 2 3) 4)
(AND 1 2 3 4)
```
Like `&key`, `&rest` modifies the way arguments are parsed. With a `&rest` in its parameter list, a function or macro can take an arbitrary number of arguments, which are collected into a single list that becomes the value of the variable whose name follows the `&rest.` So if you call `where` like this:
```lisp
(where :title "Give Us a Break" :ripped t)
```
the variable clauses will contain the list.

```lisp
(:title "Give Us a Break" :ripped t)
```
This list is passed to `make-comparisons-list`, which returns a list of comparison expressions. You can see exactly what code a call to `where` will generate using the function **MACROEXPAND-1**. If you pass **MACROEXPAND-1**, a form representing a macro call, it will call the macro code with appropriate arguments and return the expansion. So you can check out the previous `where` call like this:
```lisp
CL-USER> (macroexpand-1 '(where :title "Give Us a Break" :ripped t))
#'(LAMBDA (CD)
    (AND (EQUAL (GETF CD :TITLE) "Give Us a Break")
         (EQUAL (GETF CD :RIPPED) T)))
T
```
```lisp
CL-USER> (select (where :title "Give Us a Break" :ripped t))
((:TITLE "Give Us a Break" :ARTIST "Limpopo" :RATING 10 :RIPPED T))
```
