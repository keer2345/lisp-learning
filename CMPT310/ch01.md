**[LISP Tutorial 1: Basic LISP Programming](https://www2.cs.sfu.ca/CourseCentral/310/pwfong/Lisp/1/tutorial1.html)**

## LISP Expressions
```lisp
CL-USER> (* 2 (cos 0) (+ 4 6))
20.0
```
# Defining Functions
```lisp
CL-USER> (defun my-double (x) (* x 2))
MY-DOUBLE
CL-USER> (my-double 3)
6
```


## Editing, Loading and Compiling LISP Programs
Most of the functions we would like to write is going to be a lot longer than the double function. When working with complex programs, it is usually desirable to edit the program with an editor, fully debug the code, and then compile it for faster performance. Use your favorite text editor (mine is emacs) to key in the following function definition:

`codes/ch01/testing.lisp`:
```lisp
;;; testing.lisp
;;; by keer2345
;;;
;;; Introductory comments are preceded by ";;;"
;;; Function headers are preceded by ";;"
;;; Inline comments are introduced by ";"
;;;

;;
;; Triple the value of a number
;;

(defun triple (X)
  "Compute three times X." ;Inline comments can
  (* 3 X)) ; be place here

;;
;; Negate the sign of a number
;;

(defun negate (X)
  "Negate the value of X." ; This is a documention string.
  (- X))
```
Now load the definition into the LISP environment by typing:
```lisp
CL-USER> (load "testing.lisp")
T
CL-USER> (triple 2)
6
CL-USER> (negate 3)
-3
CL-USER> (negate -4)
4
```

When functions are fully debugged, we can also compile them into binaries:
```lisp
CL-USER> (compile-file "testing.lisp")
; compiling file "/home/qinjh/workspace/lisp/lisp-learning/CMPT310/codes/ch01/testing.lisp" (written 14 DEC 2019 03:50:26 PM):
; compiling (DEFUN TRIPLE ...)
; compiling (DEFUN NEGATE ...)

; wrote /home/qinjh/workspace/lisp/lisp-learning/CMPT310/codes/ch01/testing.fasl
; compilation finished in 0:00:00.003
#P"/home/qinjh/workspace/lisp/lisp-learning/CMPT310/codes/ch01/testing.fasl"
NIL
NIL
CL-USER> (load "testing")
T
CL-USER> (triple 3)
9
CL-USER> (negate 3)
-3
```

## Control Stuctures: Recursions and Conditionals
Now that we are equipped with all the tools for developing LISP programs, let us venture into something more interesting. Consider the definition of factorials:
1. `n! = 1`:	if n = 1
1. `n! = n * (n - 1)!`:	if n > 1

```lisp
CL-USER> (defun factorial (n)
	   (if (= n 1)
	       1
	       (* n (factorial (- n 1)))))
WARNING: redefining COMMON-LISP-USER::FACTORIAL in DEFUN
FACTORIAL
CL-USER> (factorial 5)
120
```

To better understand the last point, we can make use of the debugging facility `trace` (do not compile your code if you want to use `trace`):
```lisp
CL-USER> (trace factorial)
(FACTORIAL)
CL-USER> (factorial 4)
  0: (FACTORIAL 4)
    1: (FACTORIAL 3)
      2: (FACTORIAL 2)
        3: (FACTORIAL 1)
        3: FACTORIAL returned 1
      2: FACTORIAL returned 2
    1: FACTORIAL returned 6
  0: FACTORIAL returned 24
24
CL-USER> (untrace factorial)
T
```

**Exercise:** The N'th triangular number is defined to be 1 + 2 + 3 + ... + N. Alternatively, we could give a recursive definition of triangular number as follows:
1. `n = 1`:  if n = 1
1. `n = n + (n - 1)` : if n > 1
```lisp
CL-USER> (defun triangular (n)
	   (if (= n 1)
	       1
	       (+ n (triangular (- n 1)))))
TRIANGULAR
CL-USER> (triangular 5)
15
CL-USER> (triangular 100)
5050
```


Use the recursive definition to help you implement a linearly recursive function (triangular N) that returns the N'th triangular number. Enter your function definition into a text file. Then load it into LISP. Trace the execution of `(triangular 6)`.
```lisp
CL-USER> (trace triangular)
(TRIANGULAR)
CL-USER> (triangular 6)
  0: (TRIANGULAR 6)
    1: (TRIANGULAR 5)
      2: (TRIANGULAR 4)
        3: (TRIANGULAR 3)
          4: (TRIANGULAR 2)
            5: (TRIANGULAR 1)
            5: TRIANGULAR returned 1
          4: TRIANGULAR returned 3
        3: TRIANGULAR returned 6
      2: TRIANGULAR returned 10
    1: TRIANGULAR returned 15
  0: TRIANGULAR returned 21
21
CL-USER> (untrace triangular)
T
```
**Exercise:** Write down a recursive definition of $B^E$ (assuming that both $B$ and $E$ are non-negative integers). Then implement a linearly recursive function `(power B E)` that computes $B^E$. Enter your function definition into a text file. Then load it into LISP. Trace the execution of `(power 2 6)`.

```lisp
CL-USER> (defun power (B E)
	   "Raise B to the E'th power."
	   (if (zerop E)
	       1
	       (* B (power B (- E 1)))))
WARNING: redefining COMMON-LISP-USER::POWER in DEFUN
POWER
CL-USER> (power 3 4)
81
CL-USER> (trace power)
(POWER)
CL-USER> (power 2 6)
  0: (POWER 2 6)
    1: (POWER 2 5)
      2: (POWER 2 4)
        3: (POWER 2 3)
          4: (POWER 2 2)
            5: (POWER 2 1)
              6: (POWER 2 0)
              6: POWER returned 1
            5: POWER returned 2
          4: POWER returned 4
        3: POWER returned 8
      2: POWER returned 16
    1: POWER returned 32
  0: POWER returned 64
64
CL-USER> (untrace power)
T
```

## Multiple Recursions
Recall the definition of Fibonacci numbers:
- `Fib(n) = 1`:	for n = 0 or n = 1
- `Fib(n) = Fib(n-1) + Fib(n-2)`:	for n > 1

This definition can be directly translated to the following LISP code:
```lisp
CL-USER> (defun fibonacci (N)
	   (if (or (zerop N) (= N 1))
	       1
	       (+ (fibonacci (- N 1)) (fibonacci (- N 2)))))
FIBONACCI
CL-USER> (fibonacci 2)
2
CL-USER> (fibonacci 3)
3
```
**Exercise:** The Binomial Coefficient $B(n, r)$ is the coefficient of the term $x^r$ in the binormial expansion of $(1 + x)^n$. For example, $B(4, 2) = 6$ because $(1+x)4 = 1 + 4x + 6x^2 + 4x^3 + x^4$. The Binomial Coefficient can be computed using the Pascal Triangle formula:


- B(n, r) = 1		if r = 0 or r = n
- B(n, r) = B(n-1, r-1) + B(n-1, r)		otherwise
```lisp
;;
;; Multiple Recursions
;;

(defun binomial (N R)
  "Computer binomial coefficient B(N,R)."
  (if (or (zerop R) (= N R))
      1
      (+ (binomial (- N 1) (- R 1))
	 (binomial (- N 1) R))))
```

Implement a doubly recursive function `(binomial N R)` that computes the binomial coefficient  $B(N, R)$.
```lisp
CL-USER> (BINOMIAL 4 2)
6
CL-USER> (binomial 4 4)
1
CL-USER> (binomial 5 2)
10
```

## Lists
Constructors are forms that create new instances of a data type (possibly out of some simpler components). A list is obtained by evaluating one of the following constructors:

- `nil`: Evaluating nil creates an empty list;
- `(cons x L)`: Given a LISP object *x* and a list *L*, evaluating `(cons x L)` creates a list containing x followed by the elements in *L*.


```lisp
CL-USER> (quote (2  3 5 7 11 13 17 19))
(2 3 5 7 11 13 17 19)
CL-USER> '(2 3 5 7 11 13 17 19)
(2 3 5 7 11 13 17 19)
CL-USER> (first '(2 4 8))
2
CL-USER> (rest '(2 4 8))
(4 8)
CL-USER> (first (rest '(2 4 8)))
4
CL-USER> (rest (rest '(2 4 8)))
(8)
CL-USER> (rest (rest (rest '(2 4 8))))
NIL
```



```lisp
CL-USER(29): (null nil)
T
CL-USER(30): (null '(1 2 3))
NIL
CL-USER(31): (consp nil)
NIL
CL-USER(32): (consp '(1 2 3))
T
```
## Structural Recursion with Lists
```lisp
CL-USER> (defun recursive-list-length (L)
	   (if (null L)
	       0
	       (+ 1 (recursive-list-length (rest L)))))
RECURSIVE-LIST-LENGTH
CL-USER> (recursive-list-length  '(2 3 5 7 11 13 17 19))
8
```
**Exercise:** Implement a linearly recursive function `(sum L)` which computes the sum of all numbers in a list $L$. Compare your solution with the standard pattern of structural recursion.
```lisp
;;
;; Programming with Lists
;;

(defun sum-list (L)
  (if (null L)
      0
      (+ (car L) (sum-list (cdr L)))))
```
## Symbols
## Example: nth
```lisp
(defun list-nth (N L)
  (if (null L)
      nil
      (if (zerop N)
	  (first L)
	  (list-nth (- N 1) (rest L)))))
```
## Example: member
```lisp
(defun list-member (e l)
  (if (null l)
      nil
      (if (equal e (car l))
	  t
	  (list-member e (cdr l)))))
```
## Example: append
```lisp
(defun list-append (l1 l2)
  (if (null l1)
      l2
      (cons (car l1) (list-append (rest l1) l2))))
```
## Using Lists as Sets
```lisp
(defun list-intersection (l1 l2)
  (cond
    ((null l1) nil)
    ((member (car l1) l2)
     (cons (car l1) (list-intersection (cdr l1) l2)))
    (t (list-intersection (cdr l1) l2))))
```
